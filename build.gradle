plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "3.0.0"
    id("com.gradleup.shadow") version "9.2.2"
}

group = 'info.cho'
version = '2.6'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }

    maven{url "https://repo.codemc.org/repository/maven-public/"}

    maven { url 'https://nexus.scarsz.me/content/groups/public/' }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.5-R0.1-SNAPSHOT")

    implementation("dev.jorel:commandapi-bukkit-shade:10.1.2")

    // Extern Apis
    compileOnly("com.discordsrv:discordsrv:1.30.1")
    compileOnly ("me.clip:placeholderapi:2.11.6")
}

tasks {
    shadowJar {
        relocate("dev.jorel.commandapi", "info.cho.libs.commandapi")
    }

    runServer {
        downloadPlugins {
            url("https://www.spigotmc.org/resources/placeholderapi.6245/download?version=541946")
        }
        minecraftVersion("1.21.8")
    }

    build {
        dependsOn shadowJar
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
